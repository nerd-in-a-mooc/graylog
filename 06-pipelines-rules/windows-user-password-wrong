rule " Windows | User : wrong password"
when
    //
    // Assumes you have checked for windows-security-information
    //
    to_string($message.winlog_event_id) == "4625"             
then
    //
    // Build Alert structures : subject of alert
    //
    let subject_0 = concat("Password authentication failed: ", to_string($message.winlog_event_data_TargetUserName));
    let subject_1 = concat(subject_0, " connecting to ");
    let subject_fin = concat(subject_1, to_string($message.winlog_computer_name));
    set_field("event_digest", subject_fin);
    //
    // create detail of the alert
    //
    let LogonTypeNumber = to_string($message.winlog_event_data_LogonType);
    let LogonTypeResult = lookup_value("winLogonType",LogonTypeNumber, 0);
    let LogonTypeErr    = lookup_value("WinLogonErr" ,to_string($message.winlog_event_data_SubStatus), 0);
    let build_mess_0    = concat("Failed Password Attempt | ",  to_string($message.winlog_event_data_TargetUserName));
    let build_mess_1    = concat(build_mess_0, " attempting to log in to ");
    let build_mess_2    = concat(build_mess_1, to_string($message.winlog_computer_name));
    let build_mess_3    = concat(build_mess_2, ". Logon Type: ");
    let build_mess_4    = concat(build_mess_3, to_string(LogonTypeResult));
    let build_mess_5    = concat(build_mess_4, ". Attempt came from: ");
    let build_mess_6    = concat(build_mess_5, to_string($message.winlog_event_data_WorkstationName));
    let build_mess_7    = concat(build_mess_6, ".  ERROR: ");
    let build_mess_fin  = concat(build_mess_7, to_string(LogonTypeErr));
    set_field("event_detail", build_mess_fin);
    set_field("ALERT","TRUE");
    //
    // Route to stream Windows Alerts
    //
    route_to_stream(id: "<ID of your alert stream>");
end
